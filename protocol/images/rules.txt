/* images generated by http://bottlecaps.de/rr/ui from the rules below */

LF ::= #x0a
CR ::= #x0d

/* \u2423 was choosen for better readability */
SPACE ::= "â£" /* renders wrong in the png's, -> flip back to #x20*/
SPACE ::= #x20 

CTL ::= [#x00-#x1f]  /* actually not used */
ASCII ::= [#x20-#x7f]
char ::= ASCII - " "

stream ::= messages
messages ::= (message CR? LF) +

message_structure ::= action ( SPACE specifier ( SPACE data )? )?

action ::= "*IDN?" | "ISSE&SINE2020,SECoP," secop_version "," additional_info | "describe" | "activate" | "deactivate" | "ping"  | "do" | "change" | "read" | "describing" | "active" | "inactive" | "pong" | "done" | "changed" | "update" | "error" | "_" custom_action
specifier ::= module | module ":" (parameter|command)
data ::= request_value | data_report | error_report | structure_report

token ::=  char (char)*

name ::= [a-zA-Z_] [a-zA-Z0-9_]*

module ::= name
command ::= name
parameter ::= name

message ::= defined_requests | defined_replies /* messages which are allowed to be generated */

defined_requests ::= "*IDN?"
defined_requests ::= "describe"
defined_requests ::= "activate"
defined_requests ::= "activate" SPACE module
defined_requests ::= "deactivate"
defined_requests ::= "deactivate" SPACE module
defined_requests ::= "ping" SPACE token
defined_requests ::= "do" SPACE module ":" command SPACE argument
defined_requests ::= "change" SPACE module ":" parameter SPACE new_value
defined_requests ::= "read" SPACE module ":" parameter

defined_replies ::= "ISSE&SINE2020,SECoP," secop_version "," additional_info
defined_replies ::= "describing" SPACE "." SPACE structure_report
defined_replies ::= "active"
defined_replies ::= "active" SPACE module
defined_replies ::= "inactive"
defined_replies ::= "inactive" SPACE module
defined_replies ::= "pong" SPACE token SPACE data_report
defined_replies ::= "done" SPACE module ":" command SPACE data_report
defined_replies ::= "changed" SPACE module ":" parameter SPACE data_report
defined_replies ::= "update" SPACE module ":" parameter SPACE data_report
defined_replies ::= "error" SPACE error_class SPACE error_report

must_accept_requests ::= "*IDN?"
must_accept_requests ::= "describe"
must_accept_requests ::= "describe" SPACE ignored_value
must_accept_requests ::= "describe" SPACE ignored_value SPACE ignored_value
must_accept_requests ::= "activate"
must_accept_requests ::= "activate" SPACE module
must_accept_requests ::= "activate" SPACE module SPACE ignored_value
must_accept_requests ::= "activate" SPACE module ":" parameter
must_accept_requests ::= "activate" SPACE module ":" parameter SPACE ignored_value
must_accept_requests ::= "deactivate"
must_accept_requests ::= "deactivate" SPACE module
must_accept_requests ::= "deactivate" SPACE module SPACE ignored_value
must_accept_requests ::= "deactivate" SPACE module ":" parameter
must_accept_requests ::= "deactivate" SPACE module ":" parameter SPACE ignored_value
must_accept_requests ::= "ping"
must_accept_requests ::= "ping" SPACE token
must_accept_requests ::= "ping" SPACE token SPACE ignored_value
must_accept_requests ::= "do" SPACE module ":" command
must_accept_requests ::= "do" SPACE module ":" command SPACE argument
must_accept_requests ::= "change" SPACE module ":" parameter SPACE new_value
must_accept_requests ::= "read" SPACE module ":" parameter
must_accept_requests ::= "read" SPACE module ":" parameter SPACE ignored_value

must_accept_replies ::= "ISSE&SINE2020,SECoP," secop_version "," additional_info
must_accept_replies ::= "describing" SPACE ignored_value SPACE structure_report
must_accept_replies ::= "active"
must_accept_replies ::= "active" SPACE module
must_accept_replies ::= "active" SPACE module SPACE ignored_value
must_accept_replies ::= "active" SPACE module ":" parameter
must_accept_replies ::= "active" SPACE module ":" parameter SPACE ignored_value
must_accept_replies ::= "inactive"
must_accept_replies ::= "inactive" SPACE module
must_accept_replies ::= "inactive" SPACE module SPACE ignored_value
must_accept_replies ::= "inactive" SPACE module ":" parameter
must_accept_replies ::= "inactive" SPACE module ":" parameter SPACE ignored_value
must_accept_replies ::= "pong" SPACE SPACE data_report
must_accept_replies ::= "pong" SPACE token SPACE data_report
must_accept_replies ::= "done" SPACE module ":" command SPACE data_report
must_accept_replies ::= "changed" SPACE module ":" parameter SPACE data_report
must_accept_replies ::= "update" SPACE module ":" parameter SPACE data_report
must_accept_replies ::= "error" SPACE error_class SPACE error_report



data_report ::= "[" json-value "," qualifiers ("," ignored_value)* "]"
error_report ::= '["' copy_of_request '","' error_msg '",' error_info ("," ignored_value)* "]"
error_class ::= "NoSuchModule" | "NoSuchParameter" | "NoSuchCommand"
              | "CommandFailed" | "CommandRunning"
              | "ReadOnly" | "BadValue"
              | "CommunicationFailed"
              | "IsBusy" | "IsError" | "Disabled"
              | "ProtocolError" | "InternalError"

error_info ::= '{' ( ( 'exception' ':' exception_information | 'traceback' ':' traceback_information | custom_error_info_name ':' custom_error_info ) ( ',' ( 'exception' ':' exception_information | 'traceback' ':' traceback_information | custom_error_info_name ':' custom_error_info ) )* )? '}'

structure_report ::= SEC_node_description

SEC_node_description ::= '{' (SEC_node_property ( ',' SEC_node_property)* )? '}'
SEC_node_property ::= property |  ( '"modules":' '[' (name ',' module_description (',' name ',' module_description)*)? ']')
module_description ::= '{' (module_property ( ',' module_property)* )? '}'
module_property ::= property |  ( '"parameters":' '[' (name ',' properties (',' name ',' properties)*)? ']') |  ( '"commands":' '[' (name ',' properties (',' name ',' properties)*)? ']')
properties ::=  '{' (property ( ',' property)* )? '}'
property ::= (name ':' property_value)


accept_messages ::= '*IDN?'
           | 'ISSE&SINE2020,SECoP,' secop_version ',' additional_info
           | 'describe' ( SPACE ignored_value ( SPACE ignored_value )? )?
           | 'describing' SPACE ignored_value SPACE structure_report
           | ( 'activate' | 'deactivate' | 'active' | 'inactive' ) ( SPACE module ( ':' parameter )? ( SPACE ignored_value )? )?
           | 'do' SPACE module ':' command ( SPACE argument )?
           | 'change' SPACE module ':' parameter SPACE new_value
           | 'read' SPACE module ':' parameter ( SPACE ignored_value )?
           | 'ping' ( SPACE token ( SPACE ignored_value )? )?
           | ( 'pong' SPACE token? | 'done' SPACE module ':' command | ( 'changed' | 'update' ) SPACE module ':' parameter ) SPACE data_report
           | 'error' SPACE error_class SPACE error_report
